plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id("androidx.navigation.safeargs.kotlin")
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "unii.entertainment.movie.movieapp"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //TODO: uncomment me for UI tests - this need to inject manually koin dependencies
        //testInstrumentationRunner "unii.entertainment.movie.movieapp.util.KoinTestRunner"
    }
    buildTypes {

        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Movie App (Prod)"
            signingConfig signingConfigs.debug

        }
        debug {
            debuggable true
            minifyEnabled true
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Movie App (Development)"

        }
        qa {
            debuggable true
            minifyEnabled true
            applicationIdSuffix ".qa"
            resValue "string", "app_name", "Movie App (QA)"
            signingConfig signingConfigs.debug
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
    flavorDimensions 'API'
    productFlavors {
        apiMocked {
            dimension 'API'
            buildConfigField "String", "BASE_URL", '"https://api.themoviedb.org/3/"'
            buildConfigField "String", "API_KEY", apiKey
        }
        apiProduction {
            dimension 'API'
            buildConfigField "String", "BASE_URL", '"https://api.themoviedb.org/3/"'
            buildConfigField "String", "API_KEY", apiKey
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name == "qa") {
            if (names.contains("apiMocked")) {
                setIgnore(true)
            }
        }
    }
    testOptions {
        animationsDisabled = true
    }

}


//TODO: migrate me to kotlin-dsl
project.ext {
    koin_version = "3.0.2"
    coroutines_version = "1.4.2"
    nav_version = "2.3.5"
    appComopnent_version = "1.3.0"
    liveData_version = "2.3.1"
    constraintLayout_version = "2.0.4"
    androidxCore_version = "1.5.0"
    swipeToRefreash_version = "1.1.0"
    retrofit_version = "2.9.0"
    gsonConverter_version = "2.8.6"
    okhttp_version = "4.8.0"
    glide_version = "4.12.0"
    fragment_version = "1.3.4"
}
dependencies {


    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$androidxCore_version"
    implementation "androidx.appcompat:appcompat:$appComopnent_version"
    implementation "com.google.android.material:material:$appComopnent_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayout_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$liveData_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$liveData_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipeToRefreash_version"


// Koin main features for Android (Scope,ViewModel ...)
    implementation "io.insert-koin:koin-android:$koin_version"
// Koin Android - experimental builder extensions
    implementation "io.insert-koin:koin-android-ext:$koin_version"
    // Testing
    // testImplementation "org.koin:koin-test:$koin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Kotlin
    implementation("androidx.navigation:navigation-fragment-ktx:$nav_version")
    implementation("androidx.navigation:navigation-ui-ktx:$nav_version")

    // Feature module Support
    implementation("androidx.navigation:navigation-dynamic-features-fragment:$nav_version")


    //retrofit:
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.google.code.gson:gson:$gsonConverter_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    //TODO: change later to flow

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Testing Navigation

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation("androidx.navigation:navigation-testing:$nav_version")
    androidTestImplementation 'androidx.test:rules:1.3.0'


    androidTestImplementation "org.mockito:mockito-android:2.22.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    // debugImplementation("androidx.fragment:fragment-testing:$fragment_version")
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "com.google.truth:truth:1.1.2"


}